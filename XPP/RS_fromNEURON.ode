# Pospischil, et. al. Regular Spiking (RS) cell model
# Figure 1c

# ---- PARAMETERS ---- #

# 1 nF in section 2.1 & NEURON
par Cm=1

# Both 96 um in figure & NEURON
par Ldiam = 0.96

# from figure & NEURON
par Eleak=-70

# from section 2.2.1 Na current & NEURON
par Ena=50

# -90mV in section 2.2.1 K current
# -100mV in NEURON hoc file
par Ek=-100

# ??? Not in paper or figure
# -63mV in NEURON mod file
# -55mV in NEURON hoc file
par Vt=-55

# 1e-4  S/cm2 in figure & NEURON
par gleak=0.1

# 0.05  S/cm2 in figure & NEURON
par gna=50

# 0.005 S/cm2 in figure & NEURON
par gkd=5

# 7e-5  S/cm2 in figure & NEURON
par gm=0.07

# 4000ms in section 2.2.2
# 1000ms in NEURON hoc file
par tmax=1000

# Visually estimated del=200, dur=500
# In NEURON hoc del=300, dur=400
par Idelay=300
par Iduration=400

# 0.5nA in figure
# 0.75nA in NEURON hoc
par I=7.5

# No DC current in paper or NEURON
# Figure starts and stays at -65mV
# DC current of 0.516 needed to keep at -65mV
par Idc=0

# initial conditions
# Figure starts at -65mV
# -70mV in NEURON hoc
init V=-70


# ---- EQUATIONS ---- #

# Lateral area
area=2*PI*Ldiam/2*Ldiam

# Total Capacitance
# 0.29 nF in figure
# 0.28953 nF in NEURON
Cap=area*Cm

# Current balance
V'=(-Ileak-INa-IKd-Im+Iinput/area)/Cap

# Input current with DC, delay, and duration
Iinput=if(t>=Idelay)then(if(t<=(Idelay+Iduration))then(I)else(0))else(0)+Idc

# Leak current
Ileak=gleak*(V-Eleak)

# Na current
INa=gna*m*m*m*h*(V-Ena)

m'=am*(1-m)-bm*m
h'=ah*(1-h)-bh*h

am=(-0.32*(V-Vt-13))/(exp(-(V-Vt-13)/4)-1)
bm=(0.28*(V-Vt-40))/(exp((V-Vt-40)/5)-1)
ah=0.128*exp(-(V-Vt-17)/18)
bh=(4)/(1+exp(-(V-Vt-40)/5))

# Kd current
IKd=gkd*n*n*n*n*(V-Ek)

n'=an*(1-n)-bn*n

an=(-0.032*(V-Vt-15))/(exp(-(V-Vt-15)/5)-1)
bn=0.5*exp(-(V-Vt-10)/40)

# Km current
Im=gm*p*(V-Ek)

p'=(pinf-p)/tp

pinf=1/(1+exp(-(V+35)/10))
tp=tmax/(3.3*exp((V+35)/20)+exp(-(V+35)/20))

# integrator params
@ maxstor=80000,total=1000,bound=10000,xlo=0,xhi=1000,ylo=-75,yhi=40
@ meth=cvode,atol=0.0001,toler=0.0001,dt=0.3
done